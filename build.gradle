plugins {
    id 'java-library'
    id 'java'
    id 'com.modrinth.minotaur' version "2.8.10"
}

group 'org.geysermc.extension.thirdpartycosmetics'
version '1.0.1'

repositories {
    mavenCentral()
    maven {
        url 'https://repo.opencollab.dev/main'
    }
}

dependencies {
    testImplementation 'org.junit.jupiter:junit-jupiter-api:6.0.3'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:6.0.3'

    compileOnly 'com.google.code.gson:gson:2.13.2'

    compileOnly 'org.geysermc.geyser:api:2.9.4-SNAPSHOT'
}

jar {
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE

    archiveFileName = "${project.name}.jar"

    from {
        configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
    }
}

test {
    useJUnitPlatform()
}

java {
    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17
}

tasks.getByName("modrinth") {
    dependsOn("modrinthSyncBody")
}

modrinth {
    token = System.getenv("MODRINTH_TOKEN") ?: ""
    debugMode = (System.getenv("MODRINTH_TOKEN") == null)
    projectId = "thirdpartycosmetics"
    def versionDisplay = "${project.version}-b${System.getenv("BUILD_NUMBER")}"
    versionName = "ThirdPartyCosmetics ${versionDisplay}"
    versionNumber = versionDisplay
    versionType = "release"
    changelog = System.getenv("CHANGELOG") ?: ""
    gameVersions.addAll(["1.21.10"])
    loaders.addAll(["geyser"])
    failSilently = true

    syncBodyFrom = file("README.md").text
    uploadFile.set(tasks.getByPath("jar"))
}